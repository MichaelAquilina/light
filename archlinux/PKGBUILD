# Maintainer: Fredrik Haikarainen <fredrik.haikarainen@gmail.com>

pkgname=light
pkgver=1.1.2.r61.g9d8ca6b
pkgrel=1
pkgdesc='Application to control hardware lights, backlights, leds.'
arch=('i686' 'x86_64')
url="https://github.com/haikarainen/light"
license=('GPL3')
makedepends=('git')
conflicts=('light-git')
provides=('light-git')
source=('git+https://github.com/haikarainen/light.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/light"
  git describe --tags --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build(){
  cd "$srcdir/light"
  ./autogen.sh
  ./configure --with-udev --prefix=/usr
  make
}

package(){
  cd "$srcdir/light"
  make DESTDIR="$pkgdir/" install
} 
